grf {
	grfid: "DSB\0";
	name: string(STR_GRF_NAME);
	desc: string(STR_GRF_DESC);
	version: 0;
	min_compatible_version: 0;
}

railtypetable {
    RAIL, ELRL, 
}

cargotable {
 	PASS, MAIL, GOOD, COAL
}
// Basic template for 4 vehicle views
template tmpl_vehicle_basic(x, y) {
    // arguments x, y: coordinates of top-left corner of first sprite
    [x,      y,  8, 24,  -3, -12] //xpos ypos xsize ysize xoff yoff
    [x +  9, y, 22, 20, -14, -12]
    [x + 32, y, 32, 16, -16, -12]
    [x + 65, y, 22, 20,  -6, -12]
}

// Template for a vehicle with only 4 views (symmetric)
template tmpl_vehicle_4_views(num) {
    // argument num: Index in the graphics file, assuming vertical ordering of vehicles
    tmpl_vehicle_basic(1, 1 + 32 * num)
}

// Template for a vehicle with 8 views (non-symmetric)
template tmpl_vehicle_8_views(num, reversed) {
    // argument num: Index in the graphics file, assuming vertical ordering of vehicles
    // argument reversed: Reverse visible orientation of vehicle, if set to 1
    tmpl_vehicle_basic(reversed ? 89 : 1, 1 + 32 * num)
    tmpl_vehicle_basic(reversed ? 1 : 89, 1 + 32 * num)
}

// Template for a single vehicle sprite 
template tmpl_vehicle_single(num, xsize, ysize, xoff, yoff) {
    [1, 1 + 32 * num, xsize, ysize, xoff, yoff]
}
// AB sprites
spriteset(set_ab, "gfx/b-ab.png") { tmpl_vehicle_8_views(1, 0) }
// B sprites
spriteset(set_b, "gfx/b-ab.png") { tmpl_vehicle_4_views(0) }
// CM sprites
spriteset(set_cm, "gfx/cm.png") { tmpl_vehicle_4_views(0) }
// FC sprites
spriteset(set_fc, "gfx/fc.png") { tmpl_vehicle_8_views(0, 0) }

// FC purchase
spriteset(set_fc_purchase, "gfx/fc.png") { tmpl_vehicle_single(1, 27, 14, -14, -7) }
// === IC3 farveskift baseret på år ===

// Ny (efter 2004)
spriteset(set_ic3_new_mfa, "gfx/ic3.png") { tmpl_vehicle_8_views(0, 0) }
spriteset(set_ic3_new_mfb, "gfx/ic3.png") { tmpl_vehicle_8_views(1, 0) }
spriteset(set_ic3_new_ff,  "gfx/ic3.png") { tmpl_vehicle_4_views(2) }
spriteset(set_ic3_new_purchase, "gfx/ic3.png") { tmpl_vehicle_single(3, 93, 14, -45, -7) }

// Gammel (før 2004)
spriteset(set_ic3_old_mfa, "gfx/ic3_old.png") { tmpl_vehicle_8_views(0, 0) }
spriteset(set_ic3_old_mfb, "gfx/ic3_old.png") { tmpl_vehicle_8_views(1, 0) }
spriteset(set_ic3_old_ff,  "gfx/ic3_old.png") { tmpl_vehicle_4_views(2) }
spriteset(set_ic3_old_purchase, "gfx/ic3_old.png") { tmpl_vehicle_single(3, 93, 14, -45, -7) }

// Del 1 (MFA)
switch(FEAT_TRAINS, SELF, sw_ic3_mfa_graphics, build_year) {
    0..2003: return set_ic3_old_mfa;
    return set_ic3_new_mfa;
}

// Del 2 (FF)
switch(FEAT_TRAINS, SELF, sw_ic3_ff_graphics, build_year) {
    0..2003: return set_ic3_old_ff;
    return set_ic3_new_ff;
}

// Del 3 (MFB)
switch(FEAT_TRAINS, SELF, sw_ic3_mfb_graphics, build_year) {
    0..2003: return set_ic3_old_mfb;
    return set_ic3_new_mfb;
}

/*
// Hoved-switch vælger hvilken del i sammenstøkket (consist) der skal vises
switch(FEAT_TRAINS, SELF, sw_ic3_graphics, position_in_consist % 3) {
    0: return sw_ic3_mfa_graphics;
    1: return sw_ic3_ff_graphics;
    2: return sw_ic3_mfb_graphics;
}
*/
// === Vælg korrekt purchase-sprite ===
switch(FEAT_TRAINS, SELF, sw_ic3_purchase_graphics, current_date) {
    date(1989,1,1) .. date(2003,12,31): return set_ic3_old_purchase;
    return set_ic3_new_purchase;
}

// === Articulation, attach, osv. (samme som før) ===
switch(FEAT_TRAINS, SELF, sw_ic3_articulated_part, extra_callback_info1) {
	1: return item_train_ic3_mid;
	2: return item_train_ic3_end;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, sw_ic3_start_stop, num_vehs_in_consist) {
	1 .. 12: return CB_RESULT_NO_TEXT;
	return string(STR_IC3_CANNOT_START);
}
switch(FEAT_TRAINS, SELF, sw_ic3_can_attach_wagon, vehicle_type_id) {
	item_train_engine_ic3: return CB_RESULT_ATTACH_ALLOW;
    item_train_engine_ir4: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_IC3_CANNOT_ATTACH_OTHER);
}

// === ir4 farveskift baseret på år ===

// Gammel (før 2004)
spriteset(set_ir4_1993_er20, "gfx/ir4_1993.png") { tmpl_vehicle_8_views(0, 0) }
spriteset(set_ir4_1993_er21, "gfx/ir4_1993.png") { tmpl_vehicle_8_views(1, 0) }
spriteset(set_ir4_1993_fr22,  "gfx/ir4_1993.png") { tmpl_vehicle_4_views(2) }
spriteset(set_ir4_1993_purchase, "gfx/ir4_1993.png") { tmpl_vehicle_single(3, 128, 14, -64, -7) }
spriteset(set_ir4_1993_fr23,  "gfx/ir4_1993.png") { tmpl_vehicle_4_views(4) }

// Dsb grå (2004-2021)
spriteset(set_ir4_2004_er20, "gfx/ir4_2004.png") { tmpl_vehicle_8_views(0, 0) }
spriteset(set_ir4_2004_er21, "gfx/ir4_2004.png") { tmpl_vehicle_8_views(1, 0) }
spriteset(set_ir4_2004_fr22,  "gfx/ir4_2004.png") { tmpl_vehicle_4_views(2) }
spriteset(set_ir4_2004_purchase, "gfx/ir4_2004.png") { tmpl_vehicle_single(3, 128, 14, -64, -7) }
spriteset(set_ir4_2004_fr23,  "gfx/ir4_2004.png") { tmpl_vehicle_4_views(4) }

// Dsb Rød (2021-)
spriteset(set_ir4_2021_er20, "gfx/ir4_2021.png") { tmpl_vehicle_8_views(0, 0) }
spriteset(set_ir4_2021_er21, "gfx/ir4_2021.png") { tmpl_vehicle_8_views(1, 0) }
spriteset(set_ir4_2021_fr22,  "gfx/ir4_2021.png") { tmpl_vehicle_4_views(2) }
spriteset(set_ir4_2021_purchase, "gfx/ir4_2021.png") { tmpl_vehicle_single(3, 128, 14, -64, -7) }
spriteset(set_ir4_2021_fr23,  "gfx/ir4_2021.png") { tmpl_vehicle_4_views(4) }



// Del 1 (er20)
switch(FEAT_TRAINS, SELF, sw_ir4_er20_graphics, build_year) {
    0..2003: return set_ir4_1993_er20;
    2004..2020: return set_ir4_2004_er20;
    return set_ir4_2021_er20;
}

// Del 2 (fr22)
switch(FEAT_TRAINS, SELF, sw_ir4_fr22_graphics, build_year) {
    0..2003: return set_ir4_1993_fr22;
    2004..2020: return set_ir4_2004_fr22;
    return set_ir4_2021_fr22;
}

// Del 3 (fr23)
switch(FEAT_TRAINS, SELF, sw_ir4_fr23_graphics, build_year) {
    0..2003: return set_ir4_1993_fr23;
    2004..2020: return set_ir4_2004_fr23;
    return set_ir4_2021_fr23;
}

// Del 4 (er21)
switch(FEAT_TRAINS, SELF, sw_ir4_er21_graphics, build_year) {
    0..2003: return set_ir4_1993_er21;
    2004..2020: return set_ir4_2004_er21;
    return set_ir4_2021_er21;
}

// === Vælg korrekt purchase-sprite ===
switch(FEAT_TRAINS, SELF, sw_ir4_purchase_graphics, current_date) {
    date(1993,1,1) .. date(2003,12,31): return set_ir4_1993_purchase;
    date(2004,1,1) .. date(2020,12,31): return set_ir4_2004_purchase;
    return set_ir4_2021_purchase;
}

// === Articulation, attach, osv. (samme som før) ===
switch(FEAT_TRAINS, SELF, sw_ir4_articulated_part, extra_callback_info1) {
	1: return item_train_ir4_fr22;
    2: return item_train_ir4_fr23;
	3: return item_train_ir4_er21;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, sw_ir4_start_stop, num_vehs_in_consist) {
	1 .. 12: return CB_RESULT_NO_TEXT;
	return string(STR_IR4_CANNOT_START);
}
switch(FEAT_TRAINS, SELF, sw_ir4_can_attach_wagon, vehicle_type_id) {
	item_train_engine_ir4: return CB_RESULT_ATTACH_ALLOW;
    item_train_engine_ic3: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_IR4_CANNOT_ATTACH_OTHER);
}

// K engine
spriteset(set_k, "gfx/k.png") { tmpl_vehicle_8_views(0, 0) }

// K tender
spriteset(set_k_tender, "gfx/k.png") { tmpl_vehicle_8_views(1, 0) }

// K purchase
spriteset(set_k_purchase, "gfx/k.png") { tmpl_vehicle_single(2, 36, 11, -17, -7) }

// default graphics
switch(FEAT_TRAINS, SELF, sw_k_default, position_in_consist % 2) {
	1:      set_k_tender;
    set_k;
}

// Add tender
switch(FEAT_TRAINS, SELF, sw_k_articulated_part, extra_callback_info1) {
	1: return item_train_engine_k;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// ajust the length of the tender and engine
switch(FEAT_TRAINS, SELF, sw_k_length, position_in_vehid_chain % 2) {
	0: 6;
	3;
}

// only add steam effect to engine, not tender
switch(FEAT_TRAINS, SELF, sw_k_visual_effect_and_powered, position_in_consist % 4) {
	0: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -1, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}// ME sprites
spriteset(set_me_lighted, "gfx/diesel.png") { tmpl_vehicle_8_views(2, 0) }
// MO sprites
spriteset(set_mo, "gfx/mo.png") { tmpl_vehicle_8_views(0, 0) }

// MO purchase
spriteset(set_mo_purchase, "gfx/mo.png") { tmpl_vehicle_single(1, 32, 14, 0, -7) }// MY sprites
spriteset(set_my_wine, "gfx/diesel.png") { tmpl_vehicle_8_views(3, 0) }
spriteset(set_my_80, "gfx/diesel.png") { tmpl_vehicle_8_views(0, 0) }


switch(FEAT_TRAINS, SELF, sw_my_graphics, build_year) {
	0..1979:	return set_my_wine;
	set_my_80;
}
// MZ sprites
spriteset(set_mz_wine, "gfx/diesel.png") { tmpl_vehicle_8_views(4, 0) }
spriteset(set_mz_80, "gfx/diesel.png") { tmpl_vehicle_8_views(1, 0) }


switch(FEAT_TRAINS, SELF, sw_mz_graphics, build_year) {
	0..1979:	return set_mz_wine;
	set_mz_80;
}
// N engine
spriteset(set_n, "gfx/n.png") { tmpl_vehicle_8_views(0, 0) }

// N tender
spriteset(set_n_tender, "gfx/n.png") { tmpl_vehicle_8_views(1, 0) }

// N purchase
spriteset(set_n_purchase, "gfx/n.png") { tmpl_vehicle_single(2, 54, 13, -30, -7) }

// default graphics
switch(FEAT_TRAINS, SELF, sw_n_default, position_in_consist % 2) {
	1:      set_n_tender;
    set_n;
}

// Add tender
switch(FEAT_TRAINS, SELF, sw_n_articulated_part, extra_callback_info1) {
	1: return item_train_engine_n;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// ajust the length of the tender and engine
switch(FEAT_TRAINS, SELF, sw_n_length, position_in_vehid_chain % 2) {
	0: 8;
	6;
}

// only add steam effect to engine, not tender
switch(FEAT_TRAINS, SELF, sw_n_visual_effect_and_powered, position_in_consist % 4) {
	0: visual_effect_and_powered(VISUAL_EFFECT_STEAM, -1, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}// MM
spriteset(set_stog2_mm, "gfx/s-tog-2gen.png") { tmpl_vehicle_8_views(0, 0) }

// FU
spriteset(set_stog2_fu, "gfx/s-tog-2gen.png") { tmpl_vehicle_8_views(1, 0) }

// MU
spriteset(set_stog2_mu, "gfx/s-tog-2gen.png") { tmpl_vehicle_8_views(2, 0) }

// FS
spriteset(set_stog2_fs, "gfx/s-tog-2gen.png") { tmpl_vehicle_8_views(3, 0) }

// S-TOG 2. gen purchase
spriteset(set_stog2_purchase, "gfx/s-tog-2gen.png") { tmpl_vehicle_single(5, 58, 16, -30, -7) }



// STOG2 articulated train (MFA-FF-MFB)
switch(FEAT_TRAINS, SELF, sw_stog2_articulated_part, extra_callback_info1) {
	1 .. 3: return item_train_engine_stog2;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, sw_stog2_start_stop, num_vehs_in_consist) {
	1 .. 12: return CB_RESULT_NO_TEXT;
	return string(STR_STOG2_CANNOT_START);
}
switch(FEAT_TRAINS, SELF, sw_stog2_can_attach_wagon, vehicle_type_id) {
	item_train_engine_stog2: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_STOG2_CANNOT_ATTACH_OTHER);
}
switch(FEAT_TRAINS, SELF, sw_stog2_default, position_in_consist % 4) {
	0:      set_stog2_mm;
	1:		set_stog2_fu;
    2:      set_stog2_mu;
    set_stog2_fs;
}// AB
item(FEAT_TRAINS, item_wagon_ab) {

	property {
		name: string(ITEM_NAME_AB);
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        
		introduction_date: 		date(1983, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;

		track_type:				RAIL;
		weight:					37 ton;
		default_cargo_type:		PASS;
		cargo_age_period: 		185;

		cargo_capacity:			64;
		length:					8;

		cost_factor: 			250;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;

 		running_cost_base: 		RUNNING_COST_NONE;

        ai_special_flag: AI_FLAG_PASSENGER;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				set_ab;
	}
}// B
item(FEAT_TRAINS, item_wagon_b) {

	property {
		name: string(ITEM_NAME_B);
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        
		introduction_date: 		date(1983, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;

		track_type:				RAIL;
		weight:					37 ton;
		default_cargo_type:		PASS;
		cargo_age_period: 185;

		cargo_capacity:			64;
		length:					8;

		cost_factor: 			250;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;

 		running_cost_base: 		RUNNING_COST_NONE;

        ai_special_flag: AI_FLAG_PASSENGER;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				set_b;
	}
}// FC
item(FEAT_TRAINS, item_wagon_cm) {

	property {
		name: string(ITEM_NAME_CM);
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        
		introduction_date: 		date(1913, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;

		track_type:				RAIL;
		weight:					35 ton;
		default_cargo_type:		PASS;
		cargo_age_period: 		185;

		cargo_capacity:			72;
		length:					8;

		cost_factor: 			250;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;

		running_cost_base: 		RUNNING_COST_NONE;

        ai_special_flag: AI_FLAG_PASSENGER;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				set_cm;
	}
}// FC
item(FEAT_TRAINS, item_wagon_fc) {

	property {
		name: string(ITEM_NAME_FC);
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        
		introduction_date: 		date(1927, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;

		track_type:				RAIL;
		weight:					13 ton;
		default_cargo_type:		PASS;
		cargo_age_period: 		185;

		cargo_capacity:			58;
		length:					6;

		cost_factor: 			250;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;

 		running_cost_base: 		RUNNING_COST_NONE;

        ai_special_flag: AI_FLAG_PASSENGER;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				set_fc;
		purchase:				set_fc_purchase;
	}
}item(FEAT_TRAINS, item_train_engine_ic3) {
	
	property {
		name: string(ITEM_NAME_IC3);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1989, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.35;
		
		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_DIESEL;
		running_cost_base:		RUNNING_COST_DIESEL;
		running_cost_factor:	150;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			20;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					180 km/h;
		power:					1500;
		weight:					95 ton;
		air_drag_coefficient: 	0.06;

		default_cargo_type:		PASS;
		cargo_capacity:			50;
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	}

	graphics {
		purchase:				sw_ic3_purchase_graphics;
		articulated_part:		sw_ic3_articulated_part;
		start_stop:				sw_ic3_start_stop;
		can_attach_wagon:		sw_ic3_can_attach_wagon;
		default:				sw_ic3_mfa_graphics;
	}
}// K
// Midtervogn (FF)
item(FEAT_TRAINS, item_train_ic3_mid) {
    property {
        name: string(ITEM_NAME_IC3_MID);
        default_cargo_type: PASS;
        cargo_capacity: 60;                 // realistisk antal
        power: 0;
        weight: 35 ton;
        running_cost_base: RUNNING_COST_NONE;
        running_cost_factor: 0;
		length: 8;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		track_type:				RAIL;
    }

	graphics {
		default:				sw_ic3_ff_graphics;
	}
}

// Bagvogn (MFB)
item(FEAT_TRAINS, item_train_ic3_end) {
    property {
        name: string(ITEM_NAME_IC3_END);
        default_cargo_type: PASS;
        cargo_capacity: 40;
        power: 0;
        weight: 30 ton;
        running_cost_base: RUNNING_COST_NONE;
        running_cost_factor: 0;
		length: 8;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		track_type:				RAIL;
    }

	graphics {
		default:				sw_ic3_mfb_graphics;
	}
}item(FEAT_TRAINS, item_train_engine_ir4) {
	
	property {
		name: string(ITEM_NAME_IR4);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1993, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.35;
		
		track_type:				ELRL;
		engine_class:			ENGINE_CLASS_ELECTRIC;
		running_cost_base:		RUNNING_COST_ELECTRIC;
		running_cost_factor:	150;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			20;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					180 km/h;
		power:					1680 kW;
		weight:					133 ton;

		default_cargo_type:		PASS;
		cargo_capacity:			50;
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	}

	graphics {
		purchase:				sw_ir4_purchase_graphics;
		articulated_part:		sw_ir4_articulated_part;
		start_stop:				sw_ir4_start_stop;
		can_attach_wagon:		sw_ir4_can_attach_wagon;
		default:				sw_ir4_er20_graphics;
	}
}// K
// Midtervogn (FR22)
item(FEAT_TRAINS, item_train_ir4_fr22) {
    property {
        name: string(ITEM_NAME_IR4_FR22);
        default_cargo_type: PASS;
        cargo_capacity: 60;                 // realistisk antal
        power: 0;
        weight: 35 ton;
        running_cost_base: RUNNING_COST_NONE;
        running_cost_factor: 0;
		length: 8;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		track_type:				ELRL;
    }

	graphics {
		default:				sw_ir4_fr22_graphics;
	}
}


// Midtervogn (FR22)
item(FEAT_TRAINS, item_train_ir4_fr23) {
    property {
        name: string(ITEM_NAME_IR4_FR23);
        default_cargo_type: PASS;
        cargo_capacity: 60;                 // realistisk antal
        power: 0;
        weight: 35 ton;
        running_cost_base: RUNNING_COST_NONE;
        running_cost_factor: 0;
		length: 8;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		track_type:				ELRL;
    }

	graphics {
		default:				sw_ir4_fr23_graphics;
	}
}

// Bagvogn (MFB)
item(FEAT_TRAINS, item_train_ir4_er21) {
    property {
        name: string(ITEM_NAME_IR4_END);
        default_cargo_type: PASS;
        cargo_capacity: 40;
        power: 0;
        weight: 30 ton;
        running_cost_base: RUNNING_COST_NONE;
        running_cost_factor: 0;
		length: 8;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		track_type:				ELRL;        
    }

	graphics {
		default:				sw_ir4_er21_graphics;
	}
}//K
item(FEAT_TRAINS, item_train_engine_k) {
	
	property {
		name: string(ITEM_NAME_K);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1894, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.35;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_STEAM;
		running_cost_base:		RUNNING_COST_STEAM;
		running_cost_factor:	140;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			6;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					90 km/h;
		power:					165;
		weight:					42 ton;
		air_drag_coefficient: 	0.06;
		length: 5;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;
	}

	graphics {
		purchase:					set_k_purchase;
		articulated_part:			sw_k_articulated_part;
		length:						sw_k_length;
		default:					sw_k_default;
		visual_effect_and_powered:	sw_k_visual_effect_and_powered;
	}
}// ME
item(FEAT_TRAINS, item_train_engine_me) {
	
	property {
		name: string(ITEM_NAME_ME);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1981, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.27;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_DIESEL;
		running_cost_base:		RUNNING_COST_DIESEL;
		running_cost_factor:	115;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			28;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					175 km/h;
		power:					3300;
		weight:					122 ton;
		air_drag_coefficient: 	0.06;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				set_me_lighted;
	}
}// MO
item(FEAT_TRAINS, item_train_engine_mo) {
	
	property {
		name: string(ITEM_NAME_MO);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1936, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.33;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_DIESEL;
		running_cost_base:		RUNNING_COST_DIESEL;
		running_cost_factor:	200;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			10;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					120 km/h;
		power:					500 hp;
		weight:					55 ton;
		air_drag_coefficient: 	0.06;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 0, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				set_mo;
	}
}// MY
item(FEAT_TRAINS, item_train_engine_my) {
	
	property {
		name: string(ITEM_NAME_MY);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1951, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.30;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_DIESEL;
		running_cost_base:		RUNNING_COST_DIESEL;
		running_cost_factor:	120;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			18;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					133 km/h;
		power:					1950;
		weight:					101 ton;
		air_drag_coefficient: 	0.06;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				sw_my_graphics;
	}
}// MZ
item(FEAT_TRAINS, item_train_engine_mz) {
	
	property {
		name: string(ITEM_NAME_MZ);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1968, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			25;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.28;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_DIESEL;
		running_cost_base:		RUNNING_COST_DIESEL;
		running_cost_factor:	125;
		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			30;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					165 km/h;
		power:					3900;
		weight:					125 ton;
		air_drag_coefficient: 	0.06;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	}

	graphics {
		default:				sw_mz_graphics;
	}
}// N mark III
item(FEAT_TRAINS, item_train_engine_n) {
	
	property {
		name: string(ITEM_NAME_N);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1943, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.32;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_STEAM;
		running_cost_base:		RUNNING_COST_STEAM;
		running_cost_factor:	130;

		dual_headed:			0;
		length:					8;

		loading_speed: 			6;
		cost_factor: 			13;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					110 km/h;
		power:					1000 hp;
		weight:					84 ton;
		air_drag_coefficient: 	0.06;
		length: 5;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;
	}

	graphics {
		purchase:					set_n_purchase;
		articulated_part:			sw_n_articulated_part;
		length:						sw_n_length;
		default:					sw_n_default;
		visual_effect_and_powered:	sw_n_visual_effect_and_powered;
	}
}item(FEAT_TRAINS, item_train_engine_stog2) {
	
	property {
		name: string(ITEM_NAME_STOG2);
		
		climates_available: 	bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		introduction_date: 		date(1980, 1, 1);
		model_life: 			VEHICLE_NEVER_EXPIRES;
		vehicle_life: 			30;
		reliability_decay: 		20;
		tractive_effort_coefficient: 0.38;

		track_type:				RAIL;
		engine_class:			ENGINE_CLASS_ELECTRIC;
		running_cost_base:		RUNNING_COST_ELECTRIC;
		running_cost_factor:	70;
		dual_headed:			0;
		length:					6;

		loading_speed: 			6;
		cost_factor: 			35;
		sprite_id: 				SPRITE_ID_NEW_TRAIN;
		speed: 					180 km/h;
		power:					1500;
		weight:					95 ton;
		air_drag_coefficient: 	0.06;

		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info:   0;

		visual_effect_and_powered:	visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
	}

	graphics {
		purchase:				set_stog2_purchase;
		articulated_part:		sw_stog2_articulated_part;
		start_stop:				sw_stog2_start_stop;
		can_attach_wagon:		sw_stog2_can_attach_wagon;
		default:				sw_stog2_default;
	}
}